package yal.analyse ;

import yal.analyse.tds.*;
import yal.analyse.tds.entree.*;
import yal.analyse.tds.symbole.*;
import java.util.*;
import yal.arbre.* ;
import yal.arbre.expressions.* ;
import yal.arbre.instructions.* ;
import yal.arbre.instructions.affectation.* ;
import yal.arbre.expressions.* ;
import yal.exceptions.AnalyseSyntaxiqueException;
import yal.arbre.expressions.expressionBinaire.*;
import yal.arbre.expressions.expressionBinaire.expressionArithmetique.*;
import yal.arbre.expressions.expressionBinaire.expressionLogique.*;
import yal.arbre.expressions.expressionUnaire.*;
import yal.arbre.expressions.expressionUnaire.expressionArithmetique.*;
import yal.arbre.expressions.expressionUnaire.expressionLogique.*;

import java_cup.runtime.*;

action code {:
             :} ;

parser code {:

    public void report_error(String message, Object info){

        HashMap<Integer, String> lesTerminaux = new HashMap<>() ;
    
        lesTerminaux.put(new Integer(CodesLexicaux.DEBUT), "debut") ;
        lesTerminaux.put(new Integer(CodesLexicaux.FIN), "fin") ;
        lesTerminaux.put(new Integer(CodesLexicaux.POINTVIRGULE), ";") ;

        StringBuffer m = new StringBuffer() ;

        if (info instanceof java_cup.runtime.Symbol) {
            java_cup.runtime.Symbol s = ((java_cup.runtime.Symbol) info);

            if (s.left >= 0) {                
                m.append("\tligne : " + (s.left + 1)) ;
                if (s.right >= 0)                    
                    m.append(" colonne : " + (s.right+1)) ;
            }
            
            if (s.value != null) {
                lesTerminaux.put(CodesLexicaux.CSTENTIERE, "" + s.value) ;
            }

            if (lesTerminaux.containsKey(new Integer(s.sym))) {
                m.append(" dernier token lu : " + lesTerminaux.get(new Integer(s.sym))) ;
            }
            else {
                m.append(" expression non terminée") ;
            }

        }
        throw new AnalyseSyntaxiqueException("" + m) ;
    }

    public void report_fatal_error(String message, Object info){
        report_error(message, info);
    }
:};
   
/* ------------Declaration of Terminals and Non Terminals Section----------- */
   
/* Terminals (tokens returned by the scanner).  */

terminal Symbol    PROGRAMME, DEBUT, FIN, POINTVIRGULE, ECRIRE, LIRE, EGAL, PARFERMER, PAROUVERT, TANTQUE, FINTANTQUE,
    REPETER, SI, ALORS, SINON, FINSI, FONCTION, RETOURNE;
terminal String    IDF, CSTENTIERE, TYPE, EGALLOGIQUE, OU, ET, DIFFERENT, INFERIEUR, SUPERIEUR, NON, ADDITION, MOINS,
    DIVISION, MULTIPLICATION;
   
/* Non terminals used in the grammar section.  */

non terminal ArbreAbstrait PROG, LINST, INST, LDECL, DECL, AFF, BOUCLE, CONDITION, DECL_VAR, DECL_FONCTION, LINSTFONCTION,
        INSTFONCTION, LDECLFONCTION;
non terminal Expression EXP, OPERBINAIRE;
non terminal String PARAMETRES;

precedence left OU;
precedence left ET;
precedence left EGALLOGIQUE, DIFFERENT;
precedence left INFERIEUR, SUPERIEUR;
precedence left ADDITION, MOINS;
precedence left DIVISION;
precedence left MULTIPLICATION;
precedence left NON;

/* ----------------------------Grammar Section-------------------- */
 
start with PROG ;
/*  Programme posséde au moins une instruction, les declarations se trouve avant les instructions
    les declarations ne sont pas garder dans l'arbre abstrait mais seulement les declarations simpre
    (les intialisations a 0)
 */
PROG    ::=     PROGRAMME  IDF  DEBUT  LINST:li  FIN
                {:
                    Programme p = new Programme(0,(BlocDInstructions) li);
                    RESULT = p;
                :}
        |
                PROGRAMME  IDF  DEBUT  LDECL:ld  LINST:li  FIN
                {:
                    BlocDInstructions b = new BlocDInstructions(ldleft+1);
                    b.ajouter(ld);
                    b.ajouter(li);
                    Programme p = new Programme(0,(BlocDInstructions) b);
                    RESULT = p;
                :}
        ;

LINST   ::=     LINST:li  INST:i
                {:
                    ((BlocDInstructions)li).ajouter(i) ;
                    RESULT = li ;
                :}
        |
                INST:i
                {:
                    BlocDInstructions b = new BlocDInstructions(ileft + 1) ;
                    b.ajouter(i) ;
                    RESULT = b ;
                :}
        ;

LDECL   ::=     LDECL:li DECL:i
                {:
                    ((BlocDInstructions)li).ajouter(i) ;
                    RESULT = li ;
                :}
        |
                DECL:i
                {:
                    BlocDInstructions b = new BlocDInstructions(ileft + 1) ;
                    b.ajouter(i) ;
                    RESULT = b ;
                :}
        ;

INST    ::=     ECRIRE EXP:e POINTVIRGULE
                {: RESULT = new Ecrire(e, eleft + 1) ; :}
        |
                LIRE IDF:i POINTVIRGULE
                {: RESULT = new Lire(new Variable(ileft+1,i), ileft + 1); :}
        |
                AFF:i POINTVIRGULE
                {: RESULT = i;  :}
        |
                BOUCLE:i
                {: RESULT = i; :}
        |
                CONDITION:i
                {: RESULT = i; :}
        ;

INSTFONCTION    ::= INST:i
                {:
                    RESULT = i;
                :}
|
                RETOURNE EXP:j
                {:
                    RESULT = new Retourne(jleft+1,j);
                :}
;

LINSTFONCTION   ::= LINSTFONCTION:li INSTFONCTION:i
                {:
                    ((BlocDInstructions)li).ajouter(i) ;
                    RESULT = li ;
                 :}
|
                INSTFONCTION:i
                {:
                    BlocDInstructions b = new BlocDInstructions(ileft + 1) ;
                    b.ajouter(i) ;
                    RESULT = b ;
                :}
 ;

LDECLFONCTION   ::= LDECLFONCTION:li DECL_VAR:i
                    {:
                     ((BlocDInstructions)li).ajouter(i) ;
                     RESULT = li ;
                     :}
|
                    DECL_VAR:i
                    {:
                    TDS.getInstance().entreBlock();
                    BlocDInstructions b = new BlocDInstructions(ileft + 1) ;
                    b.ajouter(i) ;
                    RESULT = b ;
                    :}
                    ;

BOUCLE  ::=     TANTQUE EXP:e REPETER LINST:l FINTANTQUE
                {:
                BlocDInstructions inst = new BlocDInstructions(eleft + 1);
                inst.ajouter(l);
                RESULT = new Boucle(eleft+1,e,inst); :}
        ;

CONDITION ::=   SI EXP:e ALORS LINST:alors SINON LINST:sinon FINSI
                {:
                BlocDInstructions a = new BlocDInstructions(eleft + 1);
                a.ajouter(alors);
                BlocDInstructions s = new BlocDInstructions(eleft + 1);
                s.ajouter(sinon);
                RESULT = new Condition(eleft + 1, e, a, s); :}
                |
                SI EXP:e ALORS LINST:alors FINSI
                {:
                BlocDInstructions a = new BlocDInstructions(eleft + 1);
                a.ajouter(alors);
                RESULT = new Condition(eleft + 1, e, a, null); :}
                |
                SI EXP:e ALORS SINON LINST:sinon FINSI
                {:
                BlocDInstructions s = new BlocDInstructions(eleft + 1);
                s.ajouter(sinon);
                RESULT = new Condition(eleft + 1, e, null, s); :}
                |
                SI EXP:e ALORS FINSI
                {:
                RESULT = new Condition(eleft + 1, e, null, null); :}
        ;

DECL    ::= DECL_VAR:decl
        {:
             RESULT = decl;
        :}
|
            DECL_FONCTION:decl
        {:
            RESULT = decl;
        :}
        ;

DECL_VAR    ::= TYPE:t IDF:i POINTVIRGULE
                {:
                    TDS instance = TDS.getInstance();
                    instance.ajouter(new EntreeVariable(i, GestionnaireNombres.getInstance().getCompteur_blocs() ,ileft+1),
                        new SymboleVariable(instance.creerDeplacement(),t));
                    RESULT = new AffectationSimple(ileft + 1,new ConstanteEntiere("0",ileft + 1),new Variable(ileft+1,i));
                :}
;

DECL_FONCTION   ::= FONCTION IDF:i PARAMETRES DEBUT LDECLFONCTION:l LINSTFONCTION:li FIN
                {:
                    int nbFonc = GestionnaireNombres.getInstance().nouvelleFonction();
                    TDS.getInstance().ajouter(
                        new EntreeFonction(i, GestionnaireNombres.getInstance().getCompteur_blocs(), ileft+1, 0),
                        new SymboleFonction(nbFonc));

                    TDS.getInstance().sortieBlock();

                    RESULT = new Fonction(ileft+1, nbFonc, (BlocDInstructions)li);
                :}
                |
                FONCTION IDF:i PARAMETRES DEBUT LINSTFONCTION:li FIN
                {:
                    int nbFonc = GestionnaireNombres.getInstance().nouvelleFonction();
                    TDS.getInstance().entreBlock();
                    TDS.getInstance().ajouter(
                        new EntreeFonction(i, GestionnaireNombres.getInstance().getCompteur_blocs(), ileft+1, 0),
                        new SymboleFonction(nbFonc));
                    TDS.getInstance().sortieBlock();
                    RESULT = new Fonction(ileft+1, nbFonc, (BlocDInstructions)li);
                :}
;

PARAMETRES  ::= PAROUVERT PARFERMER
;

AFF     ::=     IDF:i EGAL EXP:e
                {: RESULT = new AffectationSimple(eleft + 1,e,new Variable(eleft+1,i)); :}
        ;

EXP     ::=
               OPERBINAIRE:o
               {:
               RESULT = o; :}
        |
               PAROUVERT EXP:e PARFERMER
               {: RESULT = e; :}
        |
               MOINS:e EXP:g
               {: RESULT = new Negatif(eleft + 1,g); :}
        |
               NON:e EXP:g
               {: RESULT = new Non(eleft + 1,g); :}
        |
               CSTENTIERE:c
               {: RESULT = new ConstanteEntiere(c, cleft + 1) ; :}
        |
               IDF:i
               {: RESULT = new Variable(ileft + 1,i); :}
        |
               IDF:i PAROUVERT  PARFERMER
        ;

OPERBINAIRE    ::=
               EXP:g MULTIPLICATION:e EXP:d
               {: RESULT = new Multiplication(eleft + 1,g,d); :}
        |
               EXP:g DIVISION:e EXP:d
               {: RESULT = new Division(eleft + 1,g,d); :}
        |
               EXP:g ADDITION:e EXP:d
               {: RESULT = new Addition(eleft + 1,g,d); :}
        |
               EXP:g MOINS:e EXP:d
               {: RESULT = new Soustraction(eleft + 1,g,d); :}
        |
               EXP:g INFERIEUR:e EXP:d
               {: RESULT = new Inferieur(eleft + 1,g,d); :}
        |
               EXP:g SUPERIEUR:e EXP:d
               {: RESULT = new Superieur(eleft + 1,g,d); :}
        |
               EXP:g EGALLOGIQUE:e EXP:d
               {: RESULT = new Egal(eleft + 1,g,d); :}
        |
               EXP:g DIFFERENT:e EXP:d
               {: RESULT = new Different(eleft + 1,g,d); :}
        |
               EXP:g ET:e EXP:d
               {: RESULT = new Et(eleft + 1,g,d); :}
        |
               EXP:g OU:e EXP:d
               {: RESULT = new Ou(eleft + 1,g,d); :}
        ;